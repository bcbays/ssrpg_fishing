var utils = import Games/Fishing2/Utils
var progress = import Games/Fishing2/Progress
var hirelingData = import Games/Fishing2/Data/HirelingData

var level = 0
var ownedAmount = 0

var color = hirelingData.color[0]
var baseCost = hirelingData.baseCost[0]
var costFactor = hirelingData.costFactor[0]
var maxAmount = hirelingData.maxAmount[0]
var scalesPerSecond = hirelingData.scalesPerSecond[0]
var baseMinWaitTime = hirelingData.baseWaitTimeRange[0][0]
var baseMaxWaitTime = hirelingData.baseWaitTimeRange[0][1]

var sprite = ascii
#
##_{)
#''Â¯)
###/(
asciiend

func ToString()
  return "Hireling(lvl=" + level + ",color=" + color + ",cost=" + 
  ^utils.FormatCost(RealCost()) + ")"

func Make(level)
  this.level = level
  ownedAmount = progress.hirelings[level]

  ?level >= hirelingData.minLevel & level <= hirelingData.maxLevel
    color = hirelingData.color[level]
    baseCost = hirelingData.baseCost[level]
    costFactor = hirelingData.costFactor[level]
    maxAmount = hirelingData.maxAmount[level]
    scalesPerSecond = hirelingData.scalesPerSecond[level]
    baseMinWaitTime = hirelingData.baseWaitTimeRange[level][0]
    baseMaxWaitTime = hirelingData.baseWaitTimeRange[level][1]
  :
    utils.Panic("Unsupported hireling level: " + level)

  return this

func GetMinWaitTime()
  return math.Max(baseMinWaitTime - ownedAmount * hirelingData.waitTimeReductionFactor, hirelingData.waitTimeRangeMin[0])

func GetMaxWaitTime()
  return math.Max(baseMaxWaitTime - ownedAmount * hirelingData.waitTimeReductionFactor, hirelingData.waitTimeRangeMin[1])

func GetScalesPerSecond()
  return scalesPerSecond * ownedAmount

func RealCost()
    return math.RoundToInt(baseCost * (1 + ownedAmount * costFactor))