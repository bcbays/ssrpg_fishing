var progress = import Games/Fishing2/Progress
var shopKeeper = import Games/Fishing2/ShopComponents/ShopKeeper
var _ui = import Games/Fishing2/ShopComponents/UiElements
var utils = import Games/Fishing2/Utils

var maxRodLevel = 6 // zero-indexed
var maxHirelingLevel = 6 // zero-indexed
var failedPurchaseDuration = 30 // frames

// shop animation
var shopPanelSpeed = 2
var shopPanelOvershoot = true
var shopIsHidden = true

// shop UI elements
var shopRoot = null
var rowsPanel = null
var rodsRow = null
var hirelingsRow = null
var openShopButton = null
var backButton = null
var purchaseFailedTimer = null
var purchaseFailedLabel = null

/* These arrays MUST be kept in sync by level: 0th element is the white
rod/hireling, 1st is cyan rod/hireling, and so on. */
var rods = []
var hirelings = []

func Init()
  ?shopRoot ! null
    return
  // progress.AddScales(1000000)  // DEBUG
  // the base shop panel
  shopRoot = ui.AddPanel()
  shopRoot.visible = true
  shopRoot.y = screen.h
  shopRoot.w = screen.w+2
  shopRoot.h = screen.h+2

  // shopkeeper NPC
  shopKeeper.Init(shopRoot)
  
  backButton = _ui.MakeBackButton(CloseShop)
  shopRoot.Add(backButton)
  
  // panel with item rows
  rowsPanel = ui.AddPanel()
  rowsPanel.visible = inherit
  rowsPanel.dock = top_center
  rowsPanel.anchor = top_center
  rowsPanel.x = 0
  rowsPanel.y = 6
  rowsPanel.w = 57
  rowsPanel.h = screen.h - 4
  rowsPanel.style = _ui.invisStyle
  shopRoot.Add(rowsPanel)

  // row with rods
  rodsRow = _ui.MakeShopRow(0)
  rowsPanel.Add(rodsRow)

  // row with hirelings
  hirelingsRow = _ui.MakeShopRow(7)
  rowsPanel.Add(hirelingsRow)

  /* Create all rods and their shop wrappers. By default owned rods are marked
  as "owned" instead of their cost. We mark the best owned rod as "current",
  and hide prices of all the following rods. */
  for idx = 0 .. maxRodLevel
    var rodFactory = new Games/Fishing2/Rod
    var rod = rodFactory.Make(idx)

    var shopRodFactory = new Games/Fishing2/ShopComponents/ShopItemSingle
    var shopRod = shopRodFactory.Init(rod, PurchaseRod, rodsRow)

    rods.Add(shopRod)

    ?idx < progress.rod
      shopRod.SetOwned()
    :?idx = progress.rod
      shopRod.SetCurrent()
    :?idx = progress.rod + 1
      shopRod.RevealCost()
    :
      shopRod.HideCost()

  /* Create all hirelings and their shop wrappers. We additionally hide all
  hirelings whose color corresponds to un-owned fishing rods. */
  for idx = 0 .. maxHirelingLevel
    var hirelingFactory = new Games/Fishing2/Hireling
    var hireling = hirelingFactory.Make(idx)

    var shopHireFactory = new Games/Fishing2/ShopComponents/ShopItemMulti
    var shopHire = shopHireFactory.Init(hireling, PurchaseHireling, hirelingsRow)

    hirelings.Add(shopHire)

    ?idx > progress.rod
      shopHire.Hide()
    :
      shopHire.Reveal()
  
func CloseShop()
  shopIsHidden = true
  shopPanelOvershoot = true

func PurchaseRod(button)
  var shopRod = utils.FindButtonParent(button, rods)
  ?shopRod = null
    return utils.Panic("Rod purchase failed, can't identify its button")

  var cost = shopRod.baseItem.cost
  ?progress.scales >= cost
    play buy
    progress.SubtractScales(cost)
    progress.UpgradeRod()
    var idx = rods.IndexOf(shopRod)
    // 1. Mark purchased rod as current
    shopRod.SetCurrent()
    // 2. Mark previous rod as owned
    ?idx > 0
      rods[idx - 1].SetOwned()
    // 3. Reveal the price of the next rod
    ?idx + 1 < rods.Count()
      rods[idx + 1].RevealCost()
    // 4. Reveal the next hireling
    hirelings[idx].Reveal()
  :
    play error
    MarkFailedPurchase(shopRod.costLabel)

func PurchaseHireling(button)
  var shopHire = utils.FindButtonParent(button, hirelings)
  ?shopHire = null
    return utils.Panic("Hireling purchase failed, can't identify its button")
  ?shopHire.baseItem.ownedAmount >= shopHire.baseItem.maxAmount
    // if somehow attempted to buy a maxed out hireling, do nothing
    return
  
  var cost = shopHire.baseItem.RealCost()
  ?progress.scales >= cost
    play buy
    progress.SubtractScales(cost)
    var idx = hirelings.IndexOf(shopHire)
    // 1. Increment the owned amount
    progress.AddHireling(idx)
    shopHire.baseItem.ownedAmount++
    // 2. Update the labels
    shopHire.Update()
  :
    play error
    MarkFailedPurchase(shopHire.costLabel)

func MarkFailedPurchase(label)
  ?purchaseFailedLabel = label
    // refresh the timer
    purchaseFailedTimer = time + failedPurchaseDuration
    return

  ?purchaseFailedLabel ! null
    // unmark the previous label
    purchaseFailedLabel.color = _ui.activeColor
  
  // mark the label
  label.color = _ui.errorColor
  purchaseFailedLabel = label
  purchaseFailedTimer = time + failedPurchaseDuration
  
func Update(SetGameState)
  ?shopIsHidden

    // hide the "Back" button right away
    ?backButton ! null
      backButton.visible = false

    ?shopRoot ! null
      // start the closing animation
      ?shopRoot.y < screen.h
        shopRoot.y = shopRoot.y + shopPanelSpeed
      :
        // animation is finished, return to the menu

        enable pause

        ?shopKeeper.state = done
          shopKeeper.label.text = te.xt(shopKeeper.msgReturned)

        shopRoot.y = screen.h
        shopRoot.visible = false

        SetGameState(Menu)
  :
    ?shopRoot = null
      Init()

    disable pause

    // start the opening animation
    ?shopRoot.y > -3 & shopPanelOvershoot
      shopRoot.y = shopRoot.y - shopPanelSpeed
      shopRoot.visible = true
    :
      // animation is finished, show the "Back" button
      ?backButton ! null
        backButton.visible = inherit

      ?shopRoot.y <= -3 & shopPanelOvershoot
        shopPanelOvershoot = false
      :?shopRoot.y < 0
        shopRoot.y = shopRoot.y + 1
      :
        shopRoot.y = 0
        ?shopKeeper.icon.pivotX > 2
          ?shopKeeper.state = null
            shopKeeper.icon.visible = inherit
            shopKeeper.icon.loop = true
            shopKeeper.icon.duration = 15
            shopKeeper.icon.Play()
            shopKeeper.state = walk
          ?time % 2
            shopKeeper.icon.pivotX--
        :?shopKeeper.state = walk
          shopKeeper.icon.pivotX = 0
          shopKeeper.icon.Load(shopKeeper.spriteArrived)
          shopKeeper.icon.frame = 0
          shopKeeper.icon.loop = false
          shopKeeper.icon.duration = 30
          shopKeeper.icon.Play()
          shopKeeper.state = arrived
        :?shopKeeper.state = arrived
          ?shopKeeper.icon.frame = 6
            shopKeeper.label.visible = inherit
            shopKeeper.state = done

  ?purchaseFailedTimer ! null & time >= purchaseFailedTimer
    purchaseFailedTimer = null
    purchaseFailedLabel.color = _ui.activeColor
    purchaseFailedLabel = null